
// Maven publishing

apply plugin: 'maven-publish'
apply plugin: 'signing'


ext {
  repositoryUrl = isSnapshotVersion ?
      'https://oss.sonatype.org/content/repositories/snapshots/' :
      'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
}


java {
  withSourcesJar()
}


javadoc {
  if(JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}


publishing {
  
  publications {
    
    mavenJava(MavenPublication) {
      from components.java
  
      pom {
    
        name = 'Swift Poet'
    
        url = 'https://github.com/outfoxx/swiftpoet'
        organization {
          name = 'Outfox, Inc.'
          url = 'https://outfoxx.io'
        }
        issueManagement {
          system = 'GitHub'
          url = 'https://github.com/outfoxx/swiftpoet/issues'
        }
        licenses {
          license {
            name = 'Apache License 2.0'
            url = 'https://raw.githubusercontent.com/outfoxx/swiftpoet/master/LICENSE.txt'
            distribution = 'repo'
          }
        }
        scm {
          url = 'https://github.com/outfoxx/swiftpoet'
          connection = 'scm:https://github.com/outfoxx/swiftpoet.git'
          developerConnection = 'scm:git@github.com:outfoxx/swiftpoet.git'
        }
        developers {
          developer {
            id = 'kdubb'
            name = 'Kevin Wooten'
            email = 'kevin@outfoxx.io'
          }
        }
      }
      
    }
  
  }
  
  repositories {
    maven {
      url repositoryUrl
      credentials {
        username project.findProperty('ossrhUsername') ?: ''
        password project.findProperty('ossrhPassword') ?: ''
      }
    }
  }
  
}


signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask('publishMavenJavaPublicationToMavenRepository') }
  sign publishing.publications.mavenJava
}
